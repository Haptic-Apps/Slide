plugins {
    id 'com.gladed.androidgitversion' version '0.4.13'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'com.android.application'
}

android {
    compileSdkVersion 28
    buildToolsVersion '29.0.3'

    defaultConfig {
        applicationId "me.ccrama.redditslide"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 326
        versionName androidGitVersion.name()

        multiDexEnabled true
    }

    flavorDimensions "variant"
    productFlavors {
        withGPlay {
            dimension "variant"
            buildConfigField "boolean", "isFDroid", "false"
        }
        noGPlay {
            dimension "variant"
            buildConfigField "boolean", "isFDroid", "true"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "Slide"
        }
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "Slide Debug"
        }

    }

    packagingOptions {
        exclude 'META-INF/*'
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
        // Translations are crowd-sourced
        disable 'MissingTranslation', 'ExtraTranslation',
                'StaticFieldLeak', 'ClickableViewAccessibility'
    }

    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }
}

dependencies {
/** Flavors **/
    withGPlayImplementation 'com.google.android.gms:play-services-drive:10.2.6'

/** Custom **/
    implementation 'com.github.ccrama:JRAW:9a2494892d'
    implementation 'com.github.ccrama:TedBottomPicker:496623c9b6'
    implementation 'com.github.ccrama:commonmark-java:07b304b575'
    implementation 'com.github.ccrama:PeekAndPop:09adee29f1'
    implementation 'com.github.ccrama:JReadability:bb291880a5'
    implementation 'com.github.Alexendoo:Android-RobotoTextView:f6d0eb5ac7'

/** AndroidX **/
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.interpolator:interpolator:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'

/** Third-party (Other) **/
    def exoPlayerVersion = '2.11.7'
    implementation "com.google.android.exoplayer:exoplayer-core:${exoPlayerVersion}"
    implementation "com.google.android.exoplayer:exoplayer-dash:${exoPlayerVersion}"
    implementation "com.google.android.exoplayer:extension-okhttp:${exoPlayerVersion}"
    implementation "com.google.android.exoplayer:exoplayer-ui:${exoPlayerVersion}"

    implementation 'com.google.guava:guava:29.0-android'
    //Updating material-dialogs to any newer version will break all AlertDialogWrapper
    //noinspection GradleDependency
    implementation 'com.afollestad.material-dialogs:commons:0.8.6.2' //0.9.6.0
    implementation 'com.jakewharton:process-phoenix:2.0.0'
    implementation 'com.github.ligi.snackengage:snackengage-core:0.24'
    implementation 'com.github.ligi.snackengage:snackengage-playrate:0.24'
    implementation 'com.github.rey5137:material:1.3.0'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.6.0'
    implementation 'com.cocosw:bottomsheet:1.5.0@aar'
    implementation 'com.lusfold.androidkeyvaluestore:library:0.1.0@aar'
    implementation 'org.apache.commons:commons-lang3:3.10'
    implementation 'org.apache.commons:commons-text:1.8'
    implementation 'commons-io:commons-io:2.7'
    implementation 'jp.wasabeef:blurry:3.0.0'
    implementation 'com.makeramen:roundedimageview:2.3.0'
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation 'com.sothree.slidinguppanel:library:3.4.0'
    implementation 'com.github.suckgamony.RapidDecoder:library:7cdfca47fa'
    implementation 'com.github.dasar:shiftcolorpicker:v0.5'
    implementation 'com.squareup.okhttp3:okhttp:3.12.12'
    implementation 'com.google.code.gson:gson:2.8.6'

    implementation 'com.github.ozodrukh:CircularReveal:2.1.0'
    compileOnly 'org.jetbrains:annotations:19.0.0'
    implementation 'com.mikepenz:itemanimators:1.1.0@aar'
    implementation 'com.neovisionaries:nv-websocket-client:2.9'
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'
    implementation 'me.everything:overscroll-decor-android:1.1.0'
    implementation('com.mikepenz:aboutlibraries:8.3.0', {
        exclude group: 'org.jetbrains', module: 'annotations'
    })
    implementation 'com.googlecode.mp4parser:isoparser:1.1.22'

/** Testing **/
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.robolectric:robolectric:3.8'
    testImplementation 'org.powermock:powermock-module-junit4:1.7.4'
}
